#!python
import os
import subprocess

opts = Variables([], ARGUMENTS)

# Gets the standard flags CC, CCX, etc.
env = DefaultEnvironment()

opts.Add(EnumVariable('target', "Compilation target",
                      'debug', ['d', 'debug', 'r', 'release']))
opts.Add(EnumVariable('platform', "Compilation platform",
                      '', ['', 'windows', 'osx']))
opts.Add(EnumVariable('p', "Compilation target, alias for 'platform'",
                      '', ['', 'windows', 'osx']))
opts.Add(BoolVariable('use_llvm', "Use the LLVM / Clang compiler", 'no'))
opts.Add(PathVariable('fmod_api', 'The FMOD API path', '', PathVariable.PathAccept))
opts.Add(PathVariable('target_path', 'The path where the lib is installed.', 'bin/'))
opts.Add(PathVariable('target_name', 'The library name.', 'fmod-gms2', PathVariable.PathAccept))

opts.Update(env)

fmod_api_core_headers_path = env['fmod_api'] + "/api/core/inc/"
fmod_api_studio_headers_path = env['fmod_api'] + "/api/studio/inc/"
fmod_api_core_libs_path = env['fmod_api'] + "/api/core/lib/x86/"
fmod_api_studio_libs_path = env['fmod_api'] + "/api/studio/lib/x86/"

fmodL_library = "fmodL_vc"
fmod_library = "fmod_vc"
fmodstudio_library = "fmodstudio_vc"
fmodstudioL_library = "fmodstudioL_vc"

if env['use_llvm']:
    env['CC'] = 'clang'
    env['CXX'] = 'clang++'

if env['p'] != '':
    env['platform'] = env['p']

if env['platform'] == '':
    print("No valid target platform selected.")
    quit()

if env['platform'] == "windows":

    env['target_path'] += 'win/'
    if env['target'] in ('debug', 'd'):
        env['target_path'] += 'debug/'
    else:
        env['target_path'] += 'release/'

    env.Append(ENV=os.environ)
    env.Append(CCFLAGS=['-W3', '-GR'])
   
    if env['target'] in ('debug', 'd'):
        env.Append(CCFLAGS=['-EHsc', '-MDd', '-ZI'])
    else:
        env.Append(CCFLAGS=['-O2', '-EHsc', '-MD'])

sources = []
sources.append(Glob('src/*.cpp')) 

env.Append(CPPPATH=[fmod_api_core_headers_path, fmod_api_studio_headers_path])
env.Append(LIBPATH=[fmod_api_studio_libs_path, fmod_api_core_libs_path])

if env['target'] in ('debug', 'd'):
    env.Append(LIBS=[fmodL_library, fmodstudioL_library])
else:
    env.Append(LIBS=[fmod_library, fmodstudio_library])

library = env.SharedLibrary(target=env['target_path'] + env['target_name'] , source=sources)

Default(library)

# Generates help for the -h scons option.
Help(opts.GenerateHelpText(env))